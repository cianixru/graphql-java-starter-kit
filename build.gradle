buildscript { repositories { jcenter() } }

plugins {
    id "com.diffplug.gradle.spotless" version "3.10.0"
    id "com.moowork.node" version "1.2.0"
}

task installGitHooks {
    doLast {
        if (!file('.git/hooks/pre-commit').exists()) {
            file('.git/hooks/pre-commit') << file('.pre-commit').text
            if (!System.properties['os.name'].toLowerCase().contains('windows')) {
                Runtime.getRuntime().exec("chmod -R +x .git/hooks/")
            }
        }
    }
}

tasks.matching { it.name != 'installGitHooks' }.all { Task task ->
    task.dependsOn installGitHooks
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

dependencies { compile project(':packages:server-java') }

task spinWatch(type: NodeTask) {
    script = file('node_modules/.bin/spin')
    args = ['watch']
}

task watch {
    dependsOn spinWatch
    dependsOn ':packages:server-java:bootRun'
}

defaultTasks 'watch'

spotless {
    java {
        target fileTree(rootDir) {
            include '**/*.java'
            exclude '.history/**'
            exclude '**/node_modules/**'
            exclude '**/.tmp/**'
        }
        eclipse().configFile '.codestyle.prefs'
    }
    groovy {
        target fileTree(rootDir) {
            include '**/*.gradle'
            exclude '.history/**'
            exclude '**/node_modules/**'
            exclude '**/.tmp/**'
        }
        greclipse().configFile '.codestyle.prefs'
        paddedCell()
    }
    format 'misc', {
        target fileTree(rootDir) {
            include '**/.gitignore', '**/*.md', '**/*.sh'
            exclude '.history/**'
            exclude '**/node_modules/**'
            exclude '**/.tmp/**'
        }
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}

task format {
    dependsOn 'spotlessApply'
    dependsOn 'spotlessMiscApply'
}

tasks.withType(Test) { dependsOn 'format' }

eclipse {
    classpath {
        file {
            whenMerged { classpath ->
                if (System.env['VSCODE_PID'] != null) {
                    if (!file('.vscode/settings.json').exists()) {
                        file('.vscode').mkdir();
                        file('.vscode/settings.json') << "{\n  \"editor.formatOnSave\": true\n}"
                    }
                }
            }
        }
    }
}